dnl $Id: configure.in,v 1.2 2011/02/25 15:21:29 hccho Exp $
dnl
dnl
dnl Exported and configured variables:
dnl CFLAGS
dnl LDFLAGS
dnl INCLUDES


dnl ------------------------------------------------------------------
dnl Initialization for autoconf
dnl ------------------------------------------------------------------
AC_PREREQ(2.59)
AC_INIT
AC_CONFIG_SRCDIR([src/nersuite/nersuite.cpp])
dnl AC_CONFIG_MACRO_DIR([m4])

dnl ------------------------------------------------------------------
dnl Checks for system
dnl ------------------------------------------------------------------
AC_CANONICAL_HOST
AC_AIX
AC_MINIX
AC_ISC_POSIX


dnl ------------------------------------------------------------------
dnl Initialization for automake
dnl ------------------------------------------------------------------
AM_INIT_AUTOMAKE(nersuite, 1.2.1)
AC_CONFIG_HEADERS(config.h)
AM_MAINTAINER_MODE
AM_C_PROTOTYPES


dnl ------------------------------------------------------------------
dnl Checks for program
dnl ------------------------------------------------------------------
dnl AM_PROG_CC_C_O
dnl AC_PROG_LIBTOOL
AC_PROG_CXX
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_RANLIB

dnl ------------------------------------------------------------------
dnl Initialization for variables
dnl ------------------------------------------------------------------
dnl CFLAGS="-std=c99 ${ac_save_CFLAGS}"
CFLAGS="${ac_save_CFLAGS}"
LDFLAGS="${ac_save_LDFLAGS}"
dnl INCLUDES="-I\$(top_srcdir) -I\$(top_srcdir)/include -I\$(srcdir)"
INCLUDES="-I\$(top_srcdir) -I\$(srcdir)"


dnl ------------------------------------------------------------------
dnl Checks for header files.
dnl ------------------------------------------------------------------
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h unistd.h stdint.h)


dnl ------------------------------------------------------------------
dnl Checks for typedefs, structures, and compiler characteristics.
dnl ------------------------------------------------------------------
AC_C_CONST
AC_CHECK_SIZEOF
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(unsigned short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(unsigned int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(unsigned long)

AC_CHECK_TYPES([uint8_t, uint16_t, uint32_t])

dnl ------------------------------------------------------------------
dnl Checks for debugging mode
dnl ------------------------------------------------------------------
AC_ARG_ENABLE(
  debug,
  [AS_HELP_STRING([--enable-debug],[Turn on debugging])]
)

if test "x$enable_debug" = "xyes"; then
   CFLAGS="-DDEBUG -O -g ${CFLAGS}"
else
   CFLAGS="-O3 -ffast-math ${CFLAGS}"
fi

dnl ------------------------------------------------------------------
dnl Checks for profiling mode
dnl ------------------------------------------------------------------
AC_ARG_ENABLE(
  profile,
  [AS_HELP_STRING([--enable-profile],[Turn on profiling])]
)

if test "x$enable_profile" = "xyes"; then
   CFLAGS="-DPROFILE -pg ${CFLAGS}"
fi


dnl ------------------------------------------------------------------
dnl Checks for library functions.
dnl ------------------------------------------------------------------
AC_FUNC_ALLOCA
AC_FUNC_MEMCMP
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(strdup strerror strtol strtoul)

dnl Check for math library
AC_CHECK_LIB(m, rand)

AC_ARG_WITH(
	liblbfgs,
	[AS_HELP_STRING([--with-liblbfgs=DIR],[liblbfgs directory])],
	[INCLUDES="-I${withval}/include ${INCLUDES}"; LDFLAGS="-L${withval}/lib ${LDFLAGS}"]
)
AC_CHECK_LIB(lbfgs, lbfgs)

AC_ARG_WITH(
	libcrfsuite,
	[AS_HELP_STRING([--with-libcrfsuite=DIR],[libcrfsuite directory])],
	[INCLUDES="-I${withval}/include ${INCLUDES}"; LDFLAGS="-L${withval}/lib ${LDFLAGS}"]
)
AC_CHECK_LIB(crfsuite, crfsuite_create_instance)


dnl ------------------------------------------------------------------
dnl Export variables
dnl ------------------------------------------------------------------
AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)
AC_SUBST(INCLUDES)

dnl ------------------------------------------------------------------
dnl Output the configure results.
dnl ------------------------------------------------------------------
dnl AC_CONFIG_FILES(Makefile genbinary.sh lib/cqdb/Makefile lib/crf/Makefile frontend/Makefile)
AC_CONFIG_FILES(Makefile src/tokenizer/Makefile src/gtagger/Makefile src/nersuite_common/Makefile src/nersuite/Makefile src/dictionary_compiler/Makefile src/dictionary_tagger/Makefile src/correct_mp/Makefile)
AC_OUTPUT
